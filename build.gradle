/*
 * Master Gradle build script
 *
 * Depends on bndPlugin property set by settings.gradle.
 * and bnd_* values from gradle.properties.
 */

import aQute.bnd.build.Workspace
import aQute.bnd.osgi.Constants

/* Add bnd gradle plugin as a script dependency */
buildscript {
  dependencies {
    classpath bndPlugin
  }
}

plugins {
  id "org.sonarqube" version "2.2"
}

configure(subprojects.findAll {it.name.endsWith('.ui')} ) {
  task angular(description: 'build angular bundles', group: 'Build') { task ->
    outputs.files('/build') //dir for UP TO DATE checking
	inputs.files('/static') //dir for UP TO DATE checking
	doLast {
      println ''
      println "run angular build for $task.project.name"
      println ''
	}
  }
}

allprojects {
  tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals('assemble') && project.hasProperty('angular'))
      theTask.dependsOn angular
  }
}

/* Initialize the bnd workspace */
Workspace.setDriver(Constants.BNDDRIVER_GRADLE)
Workspace.addGestalt(Constants.GESTALT_BATCH, null)
ext.bndWorkspace = new Workspace(rootDir, bnd_cnf).setOffline(gradle.startParameter.offline)

ext.cnf = rootProject.project(bnd_cnf)

/* Configure the subprojects */
subprojects {
  def bndProject = bndWorkspace.getProject(name)
  if (bndProject != null) {
    plugins.apply 'biz.aQute.bnd'
  }
}

task wrapper(type: Wrapper) {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}

